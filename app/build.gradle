apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    Properties properties = new Properties()
    properties.load(project.file('../local.properties').newDataInputStream())

    signingConfigs {
        release {
            keyAlias properties.getProperty('keystore.keyAlias')
            storeFile file(properties.getProperty('keystore.storeFile'))
            storePassword properties.getProperty('keystore.storePassword')
            keyPassword properties.getProperty('keystore.keyPassword')
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "soy.gabimoreno.audioclean"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 9
        versionName "0.4.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    bundle {
        density { enableSplit true }
        abi { enableSplit true }
        language { enableSplit false }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            splits.abi.enable = false
            splits.density.enable = false
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def arrow_version = "0.10.5"
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.browser:browser:1.2.0"

    implementation 'org.koin:koin-androidx-scope:2.1.6'
    implementation 'org.koin:koin-androidx-viewmodel:2.1.6'

    implementation "io.arrow-kt:arrow-core:$arrow_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    kapt "io.arrow-kt:arrow-meta:$arrow_version"

    implementation "com.google.code.gson:gson:2.8.6"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    implementation platform("com.google.firebase:firebase-bom:25.12.0")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-config-ktx"

    implementation "com.amplitude:android-sdk:2.25.2"
    implementation "com.squareup.okhttp3:okhttp:4.8.1"

    testImplementation 'junit:junit:4.13'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
}
